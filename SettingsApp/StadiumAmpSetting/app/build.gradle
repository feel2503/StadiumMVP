plugins {
    id 'com.android.application'

    id 'com.google.gms.google-services'

    // Add the Crashlytics Gradle plugin
    id 'com.google.firebase.crashlytics'
}
ext {
    AppVersionCode = 10260
    AppVersionName = "0.2.6"
}

android {
    compileSdk 34

    signingConfigs {
        thiscat_config {
            keyAlias "thiscat"
            keyPassword "=Eltmzpt="
            storeFile file('../../KeyStore/thiscat.keystore')
            storePassword "=Eltmzpt="
        }
    }

    defaultConfig {
        applicationId "kr.co.thiscat.stadiumampsetting"

        namespace("kr.co.thiscat.stadiumampsetting")

        minSdk 28
        targetSdk 34
        versionCode AppVersionCode
        versionName AppVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.thiscat_config
        }
        debug{
            signingConfig signingConfigs.thiscat_config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.8.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.activity:activity:1.8.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // https://mvnrepository.com/artifact/com.squareup.picasso/picasso
    implementation group: 'com.squareup.picasso', name: 'picasso', version: '2.5.2'

    // https://mvnrepository.com/artifact/com.github.bumptech.glide/glide
    implementation group: 'com.github.bumptech.glide', name: 'glide', version: '4.15.0'



    // firebase
    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:32.3.1')


    // Add the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation("com.google.firebase:firebase-crashlytics")
    implementation("com.google.firebase:firebase-analytics")


    // ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:2.18.0'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.18.0'
    implementation 'com.google.android.exoplayer:exoplayer-dash:2.18.0'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.18.0'


    modules {
        module("org.jetbrains.kotlin:kotlin-stdlib-jdk7") {
            replacedBy("org.jetbrains.kotlin:kotlin-stdlib", "kotlin-stdlib-jdk7 is now part of kotlin-stdlib")
        }
        module("org.jetbrains.kotlin:kotlin-stdlib-jdk8") {
            replacedBy("org.jetbrains.kotlin:kotlin-stdlib", "kotlin-stdlib-jdk8 is now part of kotlin-stdlib")
        }
    }

}

task releaseDeploy {
    finalizedBy {
        def name_prefix = "${rootProject.name}_${AppVersionName}"
        def release_path = '../../apk'
        def release_apk = "build/outputs/apk/release"
        if (project.hasProperty("buildDate")) {
            def buildDate = (new Date()).format('MMddHHmm')
            name_prefix = "${name_prefix}_${buildDate}"
        }

        // APK 배포
        task(deployApk, type: Copy) {
            from "${release_apk}"
            into "${release_path}"
            rename('.*.apk', "${name_prefix}.apk")
            include('**/*release.apk')
        }
    }
}

task releaseBuild(dependsOn: ['assembleRelease']) {
}

task release(dependsOn: ['clean', 'releaseBuild', 'releaseDeploy']) {
    releaseBuild.mustRunAfter clean
    releaseDeploy.mustRunAfter releaseBuild
}